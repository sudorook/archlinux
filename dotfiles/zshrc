#! /bin/zsh

#
# Global settings
#

export BROWSER="firefox"
export EDITOR="vim"
export VISUAL="${EDITOR}"


#
# Set environment variables for programming languages
#

# go
command -v go >/dev/null 2>&1 && {
  export GOPATH="${HOME}/.go"
  export PATH="${PATH}:${GOPATH}/bin"
}

# ruby
command -v ruby >/dev/null 2>&1 && {
  PATH="$(ruby -e 'print Gem.user_dir')/bin:${PATH}"
  GEM_HOME=$(ruby -e 'print Gem.user_dir')
  export PATH
  export GEM_HOME
}

# rust
command -v cargo >/dev/null 2>&1 && {
  export PATH="${PATH}:${HOME}/.cargo/bin"
}

# npm
command -v npm >/dev/null 2>&1 && {
  export PATH="${PATH}:${HOME}/.npm/bin"
  export NPM_CONFIG_PREFIX="${HOME}/.npm"
}

# R
command -v R >/dev/null 2>&1 && {
  R_LIBS_USER="${HOME}/.R/$(R --version | sed -n "s/^R version \([0-9\.]*\) .*/\1/p")"
  export R_LIBS_USER
  alias R='R --no-save '
  if [ -d /usr/share/mathjax ]; then
    export RMARKDOWN_MATHJAX_PATH=/usr/share/mathjax
  fi
}

# zsh
if [ -d ${HOME}/.local/share/zsh/site-functions ]; then
  fpath=(${HOME}/.local/share/zsh/site-functions "${fpath[@]}")
fi

# custom scripts
export PATH="${PATH}:${HOME}/.scripts"

# local executables
export PATH="${HOME}/.local/bin:${PATH}"

# local libraries
export LD_LIBRARY_PATH="${HOME}/.local/lib:/usr/local/lib:${LD_LIBRARY_PATH}"


#
# Set zsh aliases
#

alias vi='vim '
alias sudo='sudo '
alias scp='noglob scp'
alias grep='grep --color=auto'
alias diff='diff --color=auto'
alias happymake="make -j$(nproc) && sudo make install"

function superupgrade {
  sudo sh -c 'pacman -Syu && paccache -r && paccache -ruk0'
}

if command -v pacdiff >/dev/null; then
  _vimdiff="$(type vimdiff)"
  if [[ "${_vimdiff}" =~ alias ]]; then
    _vimdiff=${_vimdiff##*for }
    alias pacdiff="DIFFPROG=${_vimdiff:q} pacdiff"
    unset _vimdiff
  fi
fi

function megapurge {
  sudo sh -c 'yes | pacman -Scc' && \
    sudo journalctl --rotate && \
    sudo journalctl --vacuum-time=1s && \
    sudo pacdiff

  local pkg
  pkg=$(pacman -Qtdq | tr '\n' ' ') || true
  if [[ -n "${pkg}" ]]; then
    sudo sh -c "yes | pacman -Rs ${pkg}"
  fi
}

function make_silent {
  command -v "${1}" >/dev/null && {
    local cmd
    local bin
    bin="$(which "${1}")"
    cmd="function ${1} { local cmd=\"(${bin} \${@:q} >/dev/null 2>&1 &!)\"; eval \"\${cmd}\"; }"
    eval "${cmd}"
  }
}

function run_silent {
  command -v "${1}" >/dev/null && {
    local bin="${1}"
    local args=(${@:2})
    (${bin} ${args[@]} >/dev/null 2>&1 &!);
  }
}

make_silent eog
make_silent evince
make_silent gimp
make_silent gitg
make_silent gitk
make_silent inkscape

unset -f make_silent

{ command -v yt-dlp && ! command -v youtube-dl; } >/dev/null 2>&1 \
  && alias youtube-dl='yt-dlp '

# list directory contents
alias ls='ls --color=auto'
alias sl='ls'
alias lsa='ls -lah'
alias l='ls -lah'
alias ll='ls -lh'
alias la='ls -lAh'


#
# Color terminal output
#

# color ls output based on filetype
eval "$(dircolors -b)"

# color the man pages
man() {
  env LESS_TERMCAP_mb=$'\E[01;31m' \
  LESS_TERMCAP_md=$'\E[01;38;5;74m' \
  LESS_TERMCAP_me=$'\E[0m' \
  LESS_TERMCAP_se=$'\E[0m' \
  LESS_TERMCAP_so=$'\E[38;5;246m' \
  LESS_TERMCAP_ue=$'\E[0m' \
  LESS_TERMCAP_us=$'\E[04;38;5;146m' \
  man "$@"
}

# colored GCC warnings and errors
export GCC_COLORS='error=01;31:warning=01;35:note=01;36:caret=01;32:locus=01:quote=01'


#
# Add zsh plugins
#

include() {
  [[ -f "$1" ]] && source "$1"
}

# powerlevel10k
include /usr/share/zsh-theme-powerlevel10k/powerlevel10k.zsh-theme
include ${HOME}/.p10k.zsh

# syntax highlighting
include /usr/share/zsh/plugins/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
include /usr/share/zsh/plugins/zsh-history-substring-search/zsh-history-substring-search.zsh
include /usr/share/zsh/plugins/zsh-autosuggestions/zsh-autosuggestions.zsh

unset -f include
autoload -U compinit && compinit
